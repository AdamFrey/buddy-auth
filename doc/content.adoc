= buddy-auth - Auth facilities for ring based apps.
Andrey Antukh, <niwi@niwi.be>
0.4.1
:toc: left
:numbered:
:source-highlighter: pygments
:pygments-style: friendly
:sectlinks:

== Introduction

The _buddy-auth_ module is dedicated to providing authentication facilities for
ring and ring based web applications.


== Project Maturity

Since _buddy-auth_ is a young project there may be some API breakage.


== Install

This section covers the installation and requirements of the _buddy-auth_ library.


=== Requirements

_buddy-auth_ is tested with these platforms:

- JDK7
- JDK8


=== Leiningen

The simplest way to use _buddy-auth_ in a clojure project is by including it in  your *_project.clj_* dependency vector:

._In project.clj_
[source,clojure]
----
[buddy/buddy-auth "0.4.1"]
----

=== Gradle

If you are using gradle, this is a sample dependency declaration using the gradle dsl:

[source,groovy]
----
compile "buddy:buddy-auth:0.4.1"
----

=== Get the Code

_buddy-auth_ is open source and can be found on link:https://github.com/funcool/buddy-auth[github].

You can clone the public repository with this command:

[source,text]
----
git clone https://github.com/funcool/buddy-auth
----

== Authentication

Buddy takes a very different approach to handling authentication.
First, it clearly distinguishes authentication and authorization as two
separate steps. Second, it implements these concepts using protocols for easy
extensibility.

.Here is a list of the built-in authentication backends:
[options="header"]
|================================================
| Backend name | Namespace
| Http Basic   | `buddy.auth.backends.httpbasic`
| Session      | `buddy.auth.backends.session`
| Token        | `buddy.auth.backends.token`
| Jws Token    | `buddy.auth.backends.token`
|================================================

If you are not happy with built-in backends, you can implement your own and use it with
Buddy middleware without any problem.

Authentication in Buddy has two phases:

1. *parse*: parsing incoming request headers, parameters etc...
2. *authenticate*: having parsed data, do the authentication process, such as calling
   the auth function, unsigning the self contained token, etc.


=== Http Basic

The HTTP Basic authentication backend is one of the simplest and most insecure authentication
systems, but is a good first step for understanding how Buddy authentication works.

.Example ring handler/view
[source,clojure]
----
(require '[buddy.auth :refer (authenticated?)])
(require '[ring.util.response :refer (response)])

;; Simple ring handler. This can also be a compojure router handler
;; or anything else compatible with ring middleware.

(defn my-sample-handler
  [request]
  (if (authenticated? request)
    (response (format "Hello %s" (:identity request)))
    (response "Hello Anonymous")))
----

Http Basic requires a function with the responsibility of
identifying the incoming request which we will call *authfn*.

An *authfn* function should accept a _request_ as the first parameter
and _authdata_ as the second. _authdata_ is data parsed from the request in
the *parse* step. The *authfn* will be called only if the *parse* process
returns parsed data.

.Create an instance of the authentication backend.
[source, clojure]
----
(require '[buddy.auth.backends.httpbasic :refer [http-basic-backend]])

;; This function should return a non-nil value that
;; is automatically stored with an :identity key on the request
;; If it returns nil, a request is considered unauthenticated.

(defn my-authfn
  [request authdata]
  (let [username (:username authdata)
        password (:password authdata)]
    username))

(def backend (http-basic-backend {:realm "MyApi" :authfn my-authfn}))
----

Now having a simple view function and the backend defined, we will wrap it
with Buddy's authentication middleware.

.Declare the auth function and create a ring app with the wrapped handler.
[source,clojure]
----
(require '[buddy.auth.middleware :refer [wrap-authentication]])

;; Define the main handler with *app* name wrapping it
;; with authentication middleware using an instance of the
;; just created http-basic backend.

;; Define app var with handler wrapped with Buddy's authentication
;; middleware using the previously defined backend.

(def app (-> my-sample-handler
             (wrap-authentication backend)))
----

Now, all incoming requests with basic auth header are properly parsed
and the `:identity` key will be assoc'ed to the request map.

You can see a complete example of using this backend link:https://github.com/funcool/buddy-auth/tree/master/examples/httpbasic[here] (also with authorization).


=== Session

The session backend has the simplest implementation because it relies
entirely on ring session support.

It checks the `:identity` key in the session to authenticate the user with its
value. The value is identified as a logged-in user if it contains any logical true
value.

.Example creating a session backend instance and wrapping our handler
[source, clojure]
----
(require '[buddy.auth.backends.session :refer [session-backend]])

;; Create an instance
(def backend (session-backend))

;; Wrap the ring handler.
(def app (-> my-sample-handler
             (wrap-authentication backend)))
----

TIP: As you can see, the authentication is completely independent of login/signin. It's up to
you to implement the login handler.

You can see a complete example of using this backend link:https://github.com/funcool/buddy-auth/tree/master/examples/session[here] (also with authorization).


=== Token

This backend works much like the basic auth backend with the major difference being that this assumes
the use of tokens.

It parses a token header instead of http basic and passes it to _authfn_ for authentication.

Let's see an example:

[source, clojure]
----
(require '[buddy.auth.backends.token :refer [token-backend]])

;; Define a in-memory relation between tokens and users:
(def tokens {:2f904e245c1f5 :admin
             :45c1f5e3f05d0 :foouser})

;; Define a authfn, function with the responsibility
;; to authenticate the incoming token and return an
;; identity instance

(defn my-authfn
  [request token]
  (let [token (keyword token)]
    (get tokens token nil)))

;; Create a instance
(def backend (token-backend {:authfn my-authfn}))

;; Wrap the ring handler.
(def app (-> my-sample-handler
             (wrap-authentication backend)))
----

When "Token 2f904e245c1f5" is received in the "Authorization" header, *my-authfn* will be
executed with token as value. The responsibility of *my-authfn* is to return a valid user
or nil.

If you are so inclined, you may use a different name for your token using the **token-name** optional
key when defining your backend.

You can see a complete example of using this backend link:https://github.com/funcool/buddy-auth/tree/master/examples/token[here] (also with authorization).


=== Jws Token (Stateless)

This backend is very similar to standard token backend previously explained, but instead
of relying on _authfn_ for identify a token, it uses stateless tokens (contains all needed
data in a token, without storing any information about the token in a database or elsewhere).

This backend relies on the security of the high level signing framework for user authentication,
concretely: JWS/JWT.

Some valuable resources for learning about stateless authentication are:

- http://lucumr.pocoo.org/2013/11/17/my-favorite-database/
- http://www.niwi.be/2014/06/07/stateless-authentication-with-api-rest/

You can see a complete example of using this backend link:https://github.com/funcool/buddy-auth/tree/master/examples/token-jws[here] (also with authorization).


== Authorization

The second part of the auth process is authorization.


=== Introduction

The authorization system is split into two parts: generic authorization and access-rules
(explained in the next section).

The generic authorization consists of raising a specific exception (using **buddy.auth/throw-unauthorized**) in a ring handler to
indicate unauthorized access. This is less functional, but in some circumstances works
very well.


=== How does it work?

The authorization backend wraps everything in a try/catch block which only
handles the specific exception. When an unauthorized exception is caught, it
executes a specific function to handle it or reraises the exception.

With this approach we can define our own middlewared/decorators using custom authorization
logic with fast skip, raising an unauthorized exception using the `throw-unauthorized` function.

.Example ring handler raising an unauthorized exception.
[source, clojure]
----
(require '[buddy.auth :refer [authenticated? throw-unauthorized]])
(require '[ring.util.response :refer (response redirect)])

(defn home-controller
  [request]
  (when (not (authenticated? request))
    (throw-unauthorized {:message "Not authorized"}))
  (response "Hello World"))
----

Just like the authentication system, authorization is also implemented using protocols. All built-in authentication backends implement the protocol `IAuthorization`:

[NOTE]
====
Some authentication backends require specific behavior in the authorization layer (like http-basic
which should return a `WWW-Authenticate` header when the request is unauthorized). By default, all backends
ships with a default implementation.

You can override the default behavior by passing your own exception handler through the
`:unauthorized-handler` keyword parameter in the backend constructor.
====

Below is a complete example setting up a basic/generic authorization
system for your ring compatible web application:

.Define the final handler
[source,clojure]
----
(require '[buddy.auth.backends.httpbasic :refer [http-basic-backend]])
(require '[buddy.auth.middleware :refer [wrap-authentication wrap-authorization]])

;; Define the final handler wrapping it in the authentication and
;; authorization handlers. Use the same backend and override
;; the default unauthorized request behavior with your own defined function

(def app
  (let [backend (http-basic-backend
                 {:realm "API"
                  :authfn my-auth-fn
                  :unauthorized-handler my-unauthorized-handler})]
    (-> handler
        (wrap-authentication backend)
        (wrap-authorization backend))))
----

[[access-rules]]
== Access Rules

=== Introduction

The access rules system is another part of authorization. It consists of matching an url to
specific access rules logic.

The access rules consists of an ordered list. This contains mappings between urls and rule handlers using
link:https://github.com/weavejester/clout[clout] url matching syntax or regular expressions.

.This is an example of an access rule using the clout syntax.
[source, clojure]
----
[{:uri "/foo"
  :handler user-access}
----

.This is an example of an access rule with more than one url matching using the clout syntax.
[source, clojure]
----
[{:uris ["/foo" "/bar"]
  :handler user-access}
----

.The same example but using regular expressions.
[source, clojure]
----
[{:pattern #"^/foo$"
  :handler user-access}
----

An access rule can also match against certain HTTP methods, by using the *:request-method* option. *:request-method* can be a keyword or a set of keywords.

.An example of an access rule that matches only GET requests.
[source, clojure]
----
[{:uri "/foo"
  :handler user-access
  :request-method :get}
----

=== Rules Handlers

The rule handler is a plain function that accepts a
request as a parameter and should return `accessrules/success` or `accessrules/error`.

The `success` is a simple mark that means that handles passes the validation
and `error` is a mark that means the opposite, that the handler does not pass
the validation. Instead of returning plain boolean value, this approach allows
handlers to return errors messages or even a ring response.

.This is a simple example of the aspect of one rule handler
[source, clojure]
----
(require '[buddy.auth :refer (authenticated?)])
(require '[buddy.auth.accessrules :refer (success error)])

(defn authenticated-user
  [request]
  (if (authenticated? request)
    true
    (error "Only authenticated users allowed")))
----

These values are considered success marks: *true* and *success* instances. These are considered error marks: *nil*, *false*, and *error* instances. Error instances may contain
a string as an error message or a ring response hash-map.

Also, a rule handler can be a composition of several rule handlers using logical operators.

.This is the aspect of composition of rule-handlers
[source, clojure]
----
{:and [authenticated-user admin-user]}
{:or [authenticated-user admin-user]}
----

.Logical expressions can be nested as deep as you wish.
[source, clojure]
----
{:or [should-be-admin
      {:and [should-be-safe
             should-be-authenticated]}]}}
----

.This is an example of how a composed rule handler can be used in an access rule.
[source, clojure]
----
[{:pattern #"^/foo$"
  :handler {:and [authenticated-user admin-user]}}]
----


=== Usage

Now, knowing how rules can be defined, the question is: "`How can we use
it for access control for my routes?`"

Buddy exposes two ways to do it:

- Using a _wrap-access-rules_ middleware and defining a decoupled list of access rules.
- Using a _restrict_ decorator for assigning specific rules handlers to concrete ring handler.

Here are couple of examples of how we could do it:

.Using _wrap-access-rules_ middleware.
[source,clojure]
----
;; Rules handlers used on this example are ommited for code clarity
;; Each handler represent authorization logic indicated by its name.

(def rules [{:pattern #"^/admin/.*"
             :handler {:or [admin-access operator-access]}}
            {:pattern #"^/login$"
             :handler any-access}
            {:pattern #"^/.*"
             :handler authenticated-access}])

;; Define default behavior for not authorized requests
;;
;; This function works like a default ring compatible handler
;; and should implement the default behavior for requests
;; which are not authorized by any defined rule

(defn on-error
  [request value]
  {:status 403
   :headers {}
   :body "Not authorized"})

;; Wrap the handler with access rules (and run with jetty as example)
(defn -main
  [& args]
  (let [options {:rules rules :on-error on-error}
        app     (wrap-access-rules your-app-handler options)]
    (run-jetty app {:port 9090}))
----

If a request uri does not match any regular expression then the default policy is used.
The default policy in Buddy is *allow* but you can change the default behavior
specifying a `:reject` value in the `:policy` option.

Additionally, instead of specifying the global _on-error_ handler, you can set a specific
behavior on a specific access rule, or use the _:redirect_ option to simply redirect
a user to specific url.

.Lets see an example.
[source,clojure]
----
(def rules [{:pattern #"^/admin/.*"
             :handler {:or [admin-access operator-access]}
             :redirect "/notauthorized"}
            {:pattern #"^/login$"
             :handler any-access}
            {:pattern #"^/.*"
             :handler authenticated-access
             :on-error (fn [req _] (response "Not authorized ;)"))}])
----

The access rule options always takes precedence over the global ones.


If you don't want an external rules list and simply want to apply some rules to specific
ring views/handlers, you can use the `restrict` decorator.


.Using _restrict_ decorator.
[source, clojure]
----
(require '[buddy.auth.accessrules :refer [restrict]])

(defn home-controller
  [request]
  {:body "Hello World" :status 200})

(defroutes app
  (GET "/" [] (restrict home-controller {:handler should-be-authenticated
                                         :on-error on-error}))
----

== FAQ

*What is the difference with Friend?*

Buddy authorization/authentication facilities are more low level and less opinionated
that friend and allow build over them easy other high level abstractions.
Technically, friend abstraction can be build on top of Buddy.


*How can I use _buddy_ with link:http://clojure-liberator.github.io/liberator/[liberator]?*

By design, _buddy_ has authorization and authentication well
separated. This helps a lot if you want use only one part of it (ex:
authentication only) without including the other.

In summary: yes, you can use Buddy with liberator.


== How to Contribute?

=== Philosophy

Five most important rules:

- Beautiful is better than ugly.
- Explicit is better than implicit.
- Simple is better than complex.
- Complex is better than complicated.
- Readability counts.

All contributions to _buddy-auth_ should keep these important rules in mind.


=== Procedure

**buddy-auth** unlike Clojure and other Clojure contrib libs, does not have many
restrictions for contributions. Just follow the following steps depending on the
situation:

**Bugfix**:

- Fork the GitHub repo.
- Fix a bug/typo on a new branch.
- Make a pull-request to master.

**New feature**:

- Open new issues with the new feature proposal.
- If it is accepted, follow the same steps as "bugfix".


=== License

_buddy-auth_ is licensed under Apache 2.0 License. You can see the complete text
of the license on the root of the repository on `LICENSE` file.
