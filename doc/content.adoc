= buddy-auth - Auth facilities for ring based apps.
Andrey Antukh, <niwi@niwi.be>
:toc: left
:!numbered:
:source-highlighter: pygments
:pygments-style: friendly
:sectlinks:
:idseparator: -
:idprefix:
:toclevels: 2


== Introduction

The _buddy-auth_ module is dedicated to providing authentication facilities for
ring and ring based web applications.


== Project Maturity

Since _buddy-auth_ is a young project there may be some API breakage.


== Install

This section covers the installation and requirements of the _buddy-auth_ library.


=== Requirements

_buddy-auth_ is tested with these platforms:

- JDK7
- JDK8


=== Leiningen

The simplest way to use _buddy-auth_ in a clojure project is by including it in  your *_project.clj_* dependency vector:

._In project.clj_
[source,clojure]
----
[buddy/buddy-auth "0.5.3"]
----


=== Gradle

If you are using gradle, this is a sample dependency declaration using the gradle dsl:

[source,groovy]
----
compile "buddy:buddy-auth:0.5.3"
----

== Authentication

Buddy takes a very different approach to handling authentication. First, it clearly distinguishes
authentication and authorization as two separate steps. Second, it implements these concepts using
protocols for easy extensibility.

.Here is a list of the built-in authentication backends:
[options="header"]
|================================================
| Backend name | Namespace
| Http Basic   | `buddy.auth.backends.httpbasic`
| Session      | `buddy.auth.backends.session`
| Token        | `buddy.auth.backends.token`
| JWS Token    | `buddy.auth.backends.token`
| JWE Token    | `buddy.auth.backends.token`
|================================================

If you are not happy with built-in backends, you can implement your own and use it with
_buddy-auth_ middleware without any problem.

Authentication in _buddy-auth_ has two phases:

1. *parse*: parsing incoming request headers, parameters etc...
2. *authenticate*: having parsed data, do the authentication process, such as calling
   the auth function, unsigning the self contained token, etc.


=== Http Basic

The HTTP Basic authentication backend is one of the simplest and most insecure authentication
systems, but is a good first step for understanding how _buddy-auth_ authentication works.

.Example ring handler/view
[source,clojure]
----
(require '[buddy.auth :refer (authenticated?)])
(require '[ring.util.response :refer (response)])

;; Simple ring handler. This can also be a compojure router handler
;; or anything else compatible with ring middleware.

(defn my-sample-handler
  [request]
  (if (authenticated? request)
    (response (format "Hello %s" (:identity request)))
    (response "Hello Anonymous")))
----

The `authenticated?` function is a polymorphic function that receives a request and returns `true`
in case is already authenticated by one of _buddy-auth_ backends or `false` in case contrary.

Later, http-basic authentication backend requires a function with the responsibility of identify the
incoming request. Let see how we can do it:

[source, clojure]
----
(require '[buddy.auth.backends.httpbasic :refer [http-basic-backend]])

;; This function should return a non-nil value that
;; is automatically stored with an :identity key on the request
;; If it returns nil, a request is considered unauthenticated.

(defn my-authfn
  [request authdata]
  (let [username (:username authdata)
        password (:password authdata)]
    username))

(def backend (http-basic-backend {:realm "MyApi" :authfn my-authfn}))
----

The *authfn* function should accept a _request_ as the first parameter and _authdata_ as the second.
_authdata_ is data parsed from the request in the *parse* step.

The *authfn* will be called only if the *parse* process returns parsed data.

The next step for get it working, you should wrap your ring handler with authentication middleware:

[source,clojure]
----
(require '[buddy.auth.middleware :refer [wrap-authentication]])

;; Define the main handler with *app* name wrapping it
;; with authentication middleware using an instance of the
;; just created http-basic backend.

;; Define app var with handler wrapped with _buddy-auth_'s authentication
;; middleware using the previously defined backend.

(def app (-> my-sample-handler
             (wrap-authentication backend)))
----

All requests that reach the `my-sample-handler` will be properly processed and authenticated. In process
of authentication the `:identity` keyword will be attached to the request if a authentication is
successful.

You can see a complete example of using this backend link:https://github.com/funcool/buddy-auth/tree/master/examples/httpbasic[here] (also with authorization).


=== Session

The session backend has the simplest implementation because it relies entirely on ring session support.

The authentication process of this backend consists in checking the `:identity` keyword in session. If
it exists and is a logical true, it is automatically forwarded as is to `:identity` keyword in the
request.

.Example creating a session backend instance and wrapping our handler
[source, clojure]
----
(require '[buddy.auth.backends.session :refer [session-backend]])

;; Create an instance
(def backend (session-backend))

;; Wrap the ring handler.
(def app (-> my-sample-handler
             (wrap-authentication backend)))
----

TIP: As you can see, the authentication is completely independent of login/signin. It's up to
you to implement the login handler.

You can see a complete example of using this backend link:https://github.com/funcool/buddy-auth/tree/master/examples/session[here] (also with authorization).


=== Token

Is a backend that uses tokens for authenticate the user. It behaves very similar to the basic-auth
backend with difference that instead of authenticating with credentials it authenticate with a simple
token.

Let's see an example:

[source, clojure]
----
(require '[buddy.auth.backends.token :refer [token-backend]])

;; Define a in-memory relation between tokens and users:
(def tokens {:2f904e245c1f5 :admin
             :45c1f5e3f05d0 :foouser})

;; Define a authfn, function with the responsibility
;; to authenticate the incoming token and return an
;; identity instance

(defn my-authfn
  [request token]
  (let [token (keyword token)]
    (get tokens token nil)))

;; Create a instance
(def backend (token-backend {:authfn my-authfn}))

;; Wrap the ring handler.
(def app (-> my-sample-handler
             (wrap-authentication backend)))
----

The process of authentication of this backend consists in parse the "Authorization" header
extract the token for it, and in case of the token is extracted successful, call the `authfn`
with extracted token.

.This is a possible aspect of the authorization header
[source, text]
----
Authorization: Token 45c1f5e3f05d0
----

If you are so inclined, you may use a different name for your token using the **token-name** optional
key when defining your backend.

The `authfn` should return something that will be associated to the `:identity` key in the request.

Is the responsability of the library user to build tokens, handle its storage and verification
process. _buddy-auth_ only offers a lightweight layer over http for parsing and mark requests
as authenticated.

You can see a complete example of using this backend link:https://github.com/funcool/buddy-auth/tree/master/examples/token[here] (also with authorization).


=== JWS Token (Stateless)

Is a backend that uses signed and self contained tokens for authenticate the user. It behaves very
similar to the _Token_ backend (previously explained) with difference that this one does not need
additional user defined logic for validate tokens, because as we said previously, are self contained.

This type of tokens enables a complete stateless authentication because the server does not need any
more store the token and related information, the token will contain that information.

You can see a complete example of using this backend link:https://github.com/funcool/buddy-auth/tree/master/examples/token-jws[here] (also with authorization).

NOTE: The security and the implementation of cryptographic primitives for that token is relied to the
*buddy-sign* library (an other module of buddy) that implements the JWS specification. That library
should be used for generate JWS tokens.

Some valuable resources for learning about stateless authentication are:

- http://lucumr.pocoo.org/2013/11/17/my-favorite-database/
- http://www.niwi.be/2014/06/07/stateless-authentication-with-api-rest/


=== JWE Token (Stateless)

This backend is almost identical to the previous one (JWS), the main difference is that the backend
uses JWE (Json Web Encryption) instead of JWS (Json Web Signature) and it has the advangage that
the content of token is encrypted instead of simply signed.

This is maybe useful when token should contain some additional user information that can not be
public.

You can see a complete example of using this backend link:https://github.com/funcool/buddy-auth/tree/master/examples/token-jwe[here] (also with authorization).


== Authorization

The second part of the auth process is authorization.

The authorization system is split into two parts: generic authorization and access-rules
(explained in the next section).

The generic one is based on exceptions, and consists in raising a unauthorized exception in case
of the request is considered unauthorized. And the access rules system is based in some kind of
rules attached to handler or a _URI_ and that rules are determine if a request is autorized or not.


=== Generic

The authorization backend wraps everything in a try/catch block which only handles the specific
exception. When an unauthorized exception is caught, it executes a specific function to handle it or
reraises the exception.

With this approach we can define our own middlewared/decorators using custom authorization
logic with fast skip, raising an unauthorized exception using the `throw-unauthorized` function.

.Example ring handler raising an unauthorized exception.
[source, clojure]
----
(require '[buddy.auth :refer [authenticated? throw-unauthorized]])
(require '[ring.util.response :refer (response redirect)])

(defn home-controller
  [request]
  (when (not (authenticated? request))
    (throw-unauthorized {:message "Not authorized"}))
  (response "Hello World"))
----

Just like the authentication system, authorization is also implemented using protocols.

All built-in backends are already implements the autorization protocol with default behavior. The
default behavior can be overwritted passing the `:unauthorized-handler` option to the backend
constructor:

[source,clojure]
----
(require '[buddy.auth.backends.httpbasic :refer [http-basic-backend]])
(require '[buddy.auth.middleware :refer [wrap-authentication wrap-authorization]])

;; Simple self difined handler for unauthorized requests.
(defn my-unauthorized-handler
  [request metadata]
  (-> (response "Unauthorized request")
      (assoc :status 403)))

(def backend (http-basic-backend
              {:realm "API"
               :authfn my-auth-fn
               :unauthorized-handler my-unauthorized-handler}))

(def app (-> your-handler
             (wrap-authentication backend)
             (wrap-authorization backend)))
----

[[access-rules]]
=== Access Rules

The access rules system is another part of authorization. It consists of matching an url to
specific access rules logic.

The access rules consists of an ordered list. This contains mappings between urls and rule handlers using
link:https://github.com/weavejester/clout[clout] url matching syntax or regular expressions.

.This is an example of an access rule using the clout syntax.
[source, clojure]
----
[{:uri "/foo"
  :handler user-access}
----

.This is an example of an access rule with more than one url matching using the clout syntax.
[source, clojure]
----
[{:uris ["/foo" "/bar"]
  :handler user-access}
----

.The same example but using regular expressions.
[source, clojure]
----
[{:pattern #"^/foo$"
  :handler user-access}
----

An access rule can also match against certain HTTP methods, by using the *:request-method* option. *:request-method* can be a keyword or a set of keywords.

.An example of an access rule that matches only GET requests.
[source, clojure]
----
[{:uri "/foo"
  :handler user-access
  :request-method :get}
----


==== Rules Handlers

The rule handler is a plain function that accepts a request as a parameter and should return
`accessrules/success` or `accessrules/error`.

The `success` is a simple mark that means that handles passes the validation and `error` is a mark
that means the opposite, that the handler does not pass the validation. Instead of returning plain
boolean value, this approach allows handlers to return errors messages or even a ring response.

.This is a simple example of the aspect of one rule handler
[source, clojure]
----
(require '[buddy.auth :refer (authenticated?)])
(require '[buddy.auth.accessrules :refer (success error)])

(defn authenticated-user
  [request]
  (if (authenticated? request)
    true
    (error "Only authenticated users allowed")))
----

These values are considered success marks: *true* and *success* instances. These are considered error 
marks: *nil*, *false*, and *error* instances. Error instances may contain a string as an error message
or a ring response hash-map.

Also, a rule handler can be a composition of several rule handlers using logical operators.

.This is the aspect of composition of rule-handlers
[source, clojure]
----
{:and [authenticated-user other-handler]}
{:or [authenticated-user other-handler]}

;; Logical expressions can be nested as deep as you wish
;; with hypotetical rule handlers with self descriptive name.
{:or [should-be-admin
      {:and [should-be-safe
             should-be-authenticated]}]}}
----

This is an example of how a composed rule handler can be used in an access rules list:

[source, clojure]
----
[{:pattern #"^/foo$"
  :handler {:and [authenticated-user admin-user]}}]
----

Additionally, if you are using *clout* based syntax for matching access rules, the request in
a rule handler will contain `:match-params` with clout matched uri params.


==== Usage

Now, knowing how access rules and rule handlers can be defined, it is time to see how we can use
it in our ring applications.

_buddy-auth_ exposes two ways to do it:

* Using a _wrap-access-rules_ middleware.
* Using a _restrict_ decorator for assigning specific rules handlers to concrete ring handler.

Here are couple of examples of how we could do it:

.Using _wrap-access-rules_ middleware.
[source,clojure]
----
;; Rules handlers used on this example are ommited for code clarity
;; Each handler represent authorization logic indicated by its name.

(def rules [{:pattern #"^/admin/.*"
             :handler {:or [admin-access operator-access]}}
            {:pattern #"^/login$"
             :handler any-access}
            {:pattern #"^/.*"
             :handler authenticated-access}])

;; Define default behavior for not authorized requests
;;
;; This function works like a default ring compatible handler
;; and should implement the default behavior for requests
;; which are not authorized by any defined rule

(defn on-error
  [request value]
  {:status 403
   :headers {}
   :body "Not authorized"})

;; Wrap the handler with access rules (and run with jetty as example)
(defn -main
  [& args]
  (let [options {:rules rules :on-error on-error}
        app     (wrap-access-rules your-app-handler options)]
    (run-jetty app {:port 9090})))
----

If a request uri does not match any regular expression then the default policy is used.
The default policy in _buddy-auth_ is *allow* but you can change the default behavior
specifying a `:reject` value in the `:policy` option.

Additionally, instead of specifying the global _on-error_ handler, you can set a specific
behavior on a specific access rule, or use the _:redirect_ option to simply redirect
a user to specific url.

.Lets see an example.
[source,clojure]
----
(def rules [{:pattern #"^/admin/.*"
             :handler {:or [admin-access operator-access]}
             :redirect "/notauthorized"}
            {:pattern #"^/login$"
             :handler any-access}
            {:pattern #"^/.*"
             :handler authenticated-access
             :on-error (fn [req _] (response "Not authorized ;)"))}])
----

The access rule options always takes precedence over the global ones.

Then, if you don't want an external rules list and simply want to apply some rules to specific
ring views/handlers, you can use the `restrict` decorator. Let see it in action:

[source, clojure]
----
(require '[buddy.auth.accessrules :refer [restrict]])

(defn home-controller
  [request]
  {:body "Hello World" :status 200})

(defroutes app
  (GET "/" [] (restrict home-controller {:handler should-be-authenticated
                                         :on-error on-error}))
----


== FAQ

*What is the difference with Friend?*

_buddy-auth_ authorization/authentication facilities are more low level and less opinionated
that friend and allow build over them easy other high level abstractions.
Technically, friend abstraction can be build on top of _buddy-auth_.


*How can I use _buddy_ with link:http://clojure-liberator.github.io/liberator/[liberator]?*

By design, _buddy_ has authorization and authentication well
separated. This helps a lot if you want use only one part of it (ex:
authentication only) without including the other.

In summary: yes, you can use _buddy-auth_ with liberator.


== Developers Guide

=== Contributing

Unlike Clojure and other Clojure contributed libraries _buddy-auth_ does not have many
restrictions for contributions. Just open an issue or pull request.


=== Philosophy

Five most important rules:

- Beautiful is better than ugly.
- Explicit is better than implicit.
- Simple is better than complex.
- Complex is better than complicated.
- Readability counts.

All contributions to _buddy-auth_ should keep these important rules in mind.


=== Get the Code

_buddy-auth_ is open source and can be found on link:https://github.com/funcool/buddy-auth[github].

You can clone the public repository with this command:

[source,text]
----
git clone https://github.com/funcool/buddy-auth
----


=== Run tests

For running tests just execute this:

[source,bash]
----
lein test
----


=== License

_buddy-auth_ is licensed under Apache 2.0 License. You can see the complete text
of the license on the root of the repository on `LICENSE` file.
