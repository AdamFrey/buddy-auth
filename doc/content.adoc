= buddy-auth - Auth facilities for ring based apps.
Andrey Antukh, <niwi@niwi.be>
0.3.0
:toc: left
:numbered:
:source-highlighter: pygments
:pygments-style: friendly
:sectlinks:

== Introduction

Buddy *auth* module is dedicated for to provide authentication facilities for
ring and ring based web applications.


== Project Maturity

Since _buddy-auth_ is a young project there can be some API breakage.


== Install

This section covers the _buddy-auth_ library installing process and its requirements.


=== Requirements

_buddy-auth_ is tested with these platforms:

- JDK7
- JDK8


=== Leiningen

The simplest way to use _buddy-auth_ in a clojure project, is by including it in the dependency
vector on your *_project.clj_* file:

._on project.clj_
[source,clojure]
----
[buddy/buddy-auth "0.3.0"]
----

=== Gradle

If you are using gradle, this is a dependency line for gradle dsl:

[source,groovy]
----
compile "buddy:buddy-auth:0.3.0"
----

=== Get the Code

_buddy-auth_ is open source and can be found on link:https://github.com/funcool/buddy-auth[github].

You can clone the public repository with this command:

[source,text]
----
git clone https://github.com/funcool/buddy-auth
----

== Authentication

Buddy differs with other libraries because it takes very different approach for handling
authentication. Firstly, it clearly split authentication and authorization in two
separate steps. Secondly, it implements it using "`backends`" and protocols for easy
extensibility.

.Here is a list of built-in authentication backends:
[options="header"]
|================================================
| Backend name | Namespace
| Http Basic   | `buddy.auth.backends.httpbasic`
| Session      | `buddy.auth.backends.session`
| Token        | `buddy.auth.backends.token`
| SignedToken  | `buddy.auth.backends.token`
|================================================

If you are not happy with builtin backends, you can implement your own and use it with
buddy middlewares without any problem.

Authentication in buddy, has two phases:

1. *parse*: parsing incoming request headers, parameters etc...
2. *authenticate*: having parsed data, do the authentication process, such as call
   auth function, unsign self contained token, whatever.


=== Http Basic

The HTTP Basic authentication backend is one of the simplest and most insecure authentication
systems, but is a good first step for understand how buddy authentication works.

.Simple ring handler/view for example purpose.
[source,clojure]
----
(require '[buddy.auth :refer (authenticated?)])
(require '[ring.util.response :refer (response)])

;; Simple ring handler. This can also be a compojure router handler
;; or anything else compatible with ring middlewares.

(defn my-sample-handler
  [request]
  (if (authenticated? request)
    (response (format "Hello %s" (:identity request)))
    (response "Hello Anonymous")))
----

Http Basic requires a function with the responsability of
identify the incoming request called *authfn*.

The *authfn* function should accept a _request_ as first parameter
and _authdata_ as second. _authdata_ is a data parsed from request in
the *parse* step. The *authfn* will be called only if *parse* process
returns parsed data.

.Create an instance of authentication backend.
[source, clojure]
----
(require '[buddy.auth.backends.httpbasic :refer [http-basic-backend]])

;; This function should return a non-nil value that
;; is automatically stored on :identity key on request
;; If it returns nil, a request is considered unauthenticated.

(defn my-authfn
  [request authdata]
  (let [username (:username authdata)
        password (:password authdata)]
    username))

(def backend (http-basic-backend {:realm "MyApi" :authfn my-authfn}))
----

Now having simple view function and backend defined, you should wrap it
using the standard ring middleware way, with buddy's authentication middleware.

.Declare auth function and create ring app with wrapped handler.
[source,clojure]
----
(require '[buddy.auth.middleware :refer [wrap-authentication]])

;; Define the main handler with *app* name wrapping it
;; with authentication middleware using an instance of
;; just created http-basic backend.

;; Define app var with handler wrapped with buddy's authentication
;; middleware using just previously defined backend.

(def app (-> my-sample-handler
             (wrap-authentication backend)))
----

Now, all incoming requests with basic auth header are properly parsed
and `:identity` key will be asociated to the request map.

You can see the complete example of using this backend link:https://github.com/funcool/buddy-auth/tree/master/examples/httpbasic[here] (also with authorization).


=== Session

The session backend has the simplest implementation because it relies
entirely on ring session support.

It checks the `:identity` key in the session to authenticate the user with its
value. The value is identified as logged-in user if it contains any logical true
value.

.Simple example creating session backend instance and wrapping our handler
[source, clojure]
----
(require '[buddy.auth.backends.session :refer [session-backend]])

;; Create a instance
(def backend (session-backend))

;; Wrap your ring handler.
(def app (-> my-sample-handler
             (wrap-authentication backend)))
----

TIP: As you can view, the authentication is completely independent of login/signin. In
that case, the login handler is a respoibility of user define it.

You can see the complete example of using this backend link:https://github.com/funcool/buddy-auth/tree/master/examples/session[here] (also with authorization).


=== Token

This backend works much like the basic auth backend with the difference that this works with
tokens.

It parses a token header instead of http basic and passes it to _authfn_ for authentication.

Let see an example:

[source, clojure]
----
(require '[buddy.auth.backends.token :refer [token-backend]])

;; Define a in-memory relation between tokens and users:
(def tokens {:2f904e245c1f5 :admin
             :45c1f5e3f05d0 :foouser})

;; Define a authfn, function with the responsability
;; to authenticate the incoming token and return a
;; identity instance

(defn my-authfn
  [request token]
  (let [token (keyword token)]
    (get tokens token nil)))

;; Create a instance
(def backend (token-backend {:authfn my-authfn}))

;; Wrap your ring handler.
(def app (-> my-sample-handler
             (wrap-authentication backend)))
----

When, "Token 2f904e245c1f5" is received on "Authorization" header, *my-authfn* will be
executed with token as value. The responsability of *my-authfn* is return a valid user
or nil.


=== Signed Token (Stateless)

This backend is very similar to standard token backend previously explained, but instead
of relying on _authfn_ for identify a token, it uses stateless tokens (contains all needed
data in a token, without storing any information about token on database as ex...).

This backend relies on the security of the high level signing framework for user authentication.

Good resours for learning about that:

- http://lucumr.pocoo.org/2013/11/17/my-favorite-database/
- http://www.niwi.be/2014/06/07/stateless-authentication-with-api-rest/


== Authorization

An other part of auth process is the authorization.


=== Introduction

The authorization system is split into two parts: generic authorization and access-rules
(explained in other section).

The generic authorization consists in raising one specific exception in a ring handler for
indicate the unauthorized access. It is less functional, but in some circumstances can work
very well.


=== How does it work?

The authorization backend wraps everything in a try/catch block only
watching for specific exception, and in case of unauthorized exception is intercepted,
executes a specific function for handle it or reraise the exception.

With this approach you can define your own middlewared/decorators using custom authorization
logic with fast skip raising not authorized exception using the `throw-unauthorized` function.

.Example ring handler raising unauthorized exception.
[source, clojure]
----
(require '[buddy.auth :refer [authenticated? throw-unauthorized]])
(require '[ring.util.response :refer (response redirect)])

(defn home-controller
  [request]
  (when (not (authenticated? request))
    (throw-unauthorized {:message "Not authorized"}))
  (response "Hello World"))
----

Like authentication system, authorization is also implemented using protocols. Taking advantage of
it, all built-in authentication backends also implement this authorization protocol (`IAuthorization`):

[NOTE]
====
Some authentication backends require specific behavior in the authorization layer (like http-basic
which should return `WWW-Authenticate` header when request is unauthorized). By default, all backends
come with an specific implementation.

You can overwrite the default behavior by passing your own exception handler through the
`:unauthorized-handler` keyword parameter in the backend constructor.
====

Below is a complete example setting up a basic/generic authorization
system for your ring compatible web application:

.Define the final handler
[source,clojure]
----
(require '[buddy.auth.backends.httpbasic :refer [http-basic-backend]])
(require '[buddy.auth.middleware :refer [wrap-authentication wrap-authorization]])

;; Define the final handler wrapping it on authentication and
;; authorization handler using the same backend and overwriting
;; the default unathorized request behavior with own, previously
;; defined function

(def app
  (let [backend (http-basic-backend
                 {:realm "API"
                  :authfn my-auth-fn
                  :unauthorized-handler my-unauthorized-handler})]
    (-> handler
        (wrap-authentication backend)
        (wrap-authorization backend))))
----

[[access-rules]]
== Access Rules

=== Introduction

The access rules system is another part of the authorization, and it consists in match a url to
specific access rule logic.

The access rules consists in one ordered list, with mappings between urls and rule handler using
link:https://github.com/weavejester/clout[clout] url matching syntax o regular expressions.

.This is an example of one access rule using clout syntax.
[source, clojure]
----
[{:uri "/foo"
  :handler user-access}
----

.This is an example of one access rule with more that one url matching using clout syntax.
[source, clojure]
----
[{:uris ["/foo" "/bar"]
  :handler user-access}
----

.The same example but using regular expressions.
[source, clojure]
----
[{:pattern #"^/foo$"
  :handler user-access}
----

=== Rules Handlers

The rule handler, as unit of work is a plain function that accepts
request as parameters and should return `accessrules/success` or `accessrules/error`.

The `success` is a simple mark that means that handles passes the validation
and `error` is a mark that means the opposite, that the handler does not pases
the validation. Instead of returning plain boolean value, this approach allows
to handlers return errors messages or even an ring response.

.This is a simple example of the aspect of one rule handler
[source, clojure]
----
(require '[buddy.auth :refer (authenticated?)])
(require '[buddy.auth.accessrules :refer (success error)])

(defn authenticated-user
  [request]
  (if (authenticated? request)
    true
    (error "Only authenticated users allowed")))
----

This values are considered success marks: *true* and *success* instance. And that ones are
considered error marks: *nil*, *false*, *error* instance. Error instances can contain
a string as error message or ring response hash-map.

Also, a rule handler can be a composition of few ones using logical operators.

.This is the aspect of composition of rule-handlers
[source, clojure]
----
{:and [authenticated-user admin-user]}
{:or [authenticated-user admin-user]}
----

.Logical expressions can be nestest as deep as you want.
[source, clojure]
----
{:or [should-be-admin
      {:and [should-be-safe
             should-be-authenticated]}]}}
----

.This is an example of how an composed rule handler can be used in an access rule.
[source, clojure]
----
[{:pattern #"^/foo$"
  :handler {:and [authenticated-user admin-user]}}]
----


=== Usage

Now, knowing how rules can be defined, the question is: "`How can we use
it for access control for my routes?`"

Buddy exposes two ways to do it:

- Using a _wrap-access-rules_ middleware and define a decoupled list access rules.
- Using a _restrict_ decorator for set specific rules handlers to concrete ring handler.

Here are comple examples of how we can done it:

.Using _wrap-access-rules_ middleware.
[source,clojure]
----
;; Rules handlers used on this example are omited for code clarity
;; and them repsents a authorization logic for its name.

(def rules [{:pattern #"^/admin/.*"
             :handler {:or [admin-access operator-access]}}
            {:pattern #"^/login$"
             :handler any-access}
            {:pattern #"^/.*"
             :handler authenticated-access}])

;; Define default behavior for not authorized requests
;;
;; This functions works like default ring compatible handler
;; and should implement the default behavior for request
;; that are not authorized by any defined rule

(defn on-error
  [request value]
  {:status 403
   :headers {}
   :body "Not authorized"})

;; Wrap your handler with access rules (and run with jetty as example)
(defn -main
  [& args]
  (let [options {:rules rules :on-error on-error}
        app     (wrap-access-rules your-app-handler options)]
    (run-jetty app {:port 9090}))
----

If request uri does not match any regular expression then the default policy is used
Default policy in buddy is *allow* but you can change the default behavior
specify `:reject` value to `:policy` option.

Additionally, instead of specify the global _on-error_ handler, you can set a specific
behavior on specific access rule, o use the _:redirect_ option for simply redirect
a user to specific url.

.Let see an example.
[source,clojure]
----
(def rules [{:pattern #"^/admin/.*"
             :handler {:or [admin-access operator-access]}
             :redirect "/notauthorized"}
            {:pattern #"^/login$"
             :handler any-access}
            {:pattern #"^/.*"
             :handler authenticated-access
             :on-error (fn [req _] (response "Not authorized ;)"))}])
----

The access rule options are always has more preference that global ones.


If you don't want an external rules list and simple want apply some rules to specific
ring views/handlers, your can use `restrict` decorator.


.Using _restrict_ decorator.
[source, clojure]
----
(require '[buddy.auth.accessrules :refer [restrict]])

(defn home-controller
  [request]
  {:body "Hello World" :status 200})

(defroutes app
  (GET "/" [] (restrict home-controller {:handler should-be-authenticated
                                         :on-error on-error}))
----

== How to Contribute?

=== Philosophy

Five most important rules:

- Beautiful is better than ugly.
- Explicit is better than implicit.
- Simple is better than complex.
- Complex is better than complicated.
- Readability counts.

All contributions to _buddy-auth_ should keep these important rules in mind.


=== Procedure

**buddy-auth** unlike Clojure and other Clojure contrib libs, does not have many
restrictions for contributions. Just follow the following steps depending on the
situation:

**Bugfix**:

- Fork the GitHub repo.
- Fix a bug/typo on a new branch.
- Make a pull-request to master.

**New feature**:

- Open new issue with the new feature proposal.
- If it is accepted, follow the same steps as "bugfix".

=== License

_buddy-auth_ is licensed under BSD (2-Clause) license:

----
Copyright (c) 2013-2015, Andrey Antukh <niwi@niwi.be>

All rights reserved.

Redistribution and use in source and binary forms, with or without
modification, are permitted provided that the following conditions are met:

* Redistributions of source code must retain the above copyright notice, this
  list of conditions and the following disclaimer.

* Redistributions in binary form must reproduce the above copyright notice,
  this list of conditions and the following disclaimer in the documentation
  and/or other materials provided with the distribution.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
----
